#version 450 core
precision highp float;

#define PI 3.141592653589793238

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(rgba32f, binding = 0) uniform image2D u_Hkt_Dy;
layout(rgba32f, binding = 1) uniform image2D u_Hkt_Dx;
layout(rgba32f, binding = 2) uniform image2D u_Hkt_Dz;
layout(rgba32f, binding = 3) uniform image2D u_H0k;
layout(rgba32f, binding = 4) uniform image2D u_H0minusk;

const int L = 1000;

uniform float u_Time;

struct complex
{
	float real;
	float im;
};

complex mul(complex c0, complex c1)
{
	complex c;
	c.real = c0.real * c1.real - c0.im * c1.im;
	c.im = c0.real * c1.im + c0.im * c1.real;
	return c;
}

complex add(complex c0, complex c1)
{
	complex c;
	c.real = c0.real + c1.real;
	c.im = c0.im + c1.im;
	return c;
}

complex conj(complex c)
{
	complex c_conj = complex(c.real, -c.im);
	return c_conj;
}

void main()
{
	float t = u_Time;
	vec2 x = ivec2(gl_GlobalInvocationID.xy);

	vec2 k = vec2(2.0 * PI * x.x/L, 2.0 * PI * x.y/L);

	float magnitude = length(k);
	if(magnitude < 0.0001) magnitude = 0.0001;

	float w = sqrt(9.81 * magnitude);

	complex h0k = complex(imageLoad(u_H0k, ivec2(gl_GlobalInvocationID.xy)).r,
				imageLoad(u_H0k, ivec2(gl_GlobalInvocationID.xy)).g);

	complex h0minuskconj = conj(complex(imageLoad(u_H0minusk, ivec2(gl_GlobalInvocationID.xy)).r,
				imageLoad(u_H0minusk, ivec2(gl_GlobalInvocationID.xy)).g));

	float cosinus = cos(w*t);
	float sinus = sin(w*t);

	complex exp_iwt = complex(cosinus, sinus);
	complex exp_iwt_inv = complex(cosinus, -sinus);

	complex h_k_t_dy = add(mul(h0k, exp_iwt), (mul(h0minuskconj, exp_iwt_inv)));

	complex dx = complex(0.0, -k.x/magnitude);
	complex h_k_t_dx = mul(dx, h_k_t_dy);

	complex dy = complex(0.0, -k.y/magnitude);
	complex h_k_t_dz = mul(dy, h_k_t_dy);

	imageStore(u_Hkt_Dy, ivec2(gl_GlobalInvocationID.xy), vec4(h_k_t_dy.real, h_k_t_dy.im, 0, 1));
	imageStore(u_Hkt_Dx, ivec2(gl_GlobalInvocationID.xy), vec4(h_k_t_dx.real, h_k_t_dx.im, 0, 1));
	imageStore(u_Hkt_Dz, ivec2(gl_GlobalInvocationID.xy), vec4(h_k_t_dz.real, h_k_t_dz.im, 0, 1));
}